plugins {
    id 'jacoco'
    id 'idea'
    id 'org.jetbrains.kotlin.jvm' version '1.8.22'
    id 'org.jetbrains.kotlin.kapt' version '1.8.22'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.8.22'
    id 'org.jetbrains.kotlin.plugin.jpa' version '1.8.22'
    id 'org.springframework.boot' version '2.7.18'
    id 'com.palantir.git-version' version '0.12.0-rc2'
    id 'io.zensoft.versioning' version '1.1.0'
}

apply plugin: 'io.spring.dependency-management'

group = 'io.openfuture.api'
sourceCompatibility = 17
targetCompatibility = 17

repositories {
    mavenCentral()
}

ext {
    s3Version = "1.12.261"
}

dependencies {

    // Spring
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-freemarker")
    implementation("org.springframework.boot:spring-boot-starter-security")
    implementation("org.springframework.boot:spring-boot-starter-data-jpa")
    implementation("org.springframework.boot:spring-boot-starter-validation")

    // OAuth
    implementation("org.springframework.security:spring-security-oauth2-client")
    implementation("org.springframework.security:spring-security-oauth2-jose")

    // Database
    runtimeOnly("org.postgresql:postgresql")
    implementation("org.flywaydb:flyway-core")

    // Ethereum
    implementation("org.web3j:web3j-spring-boot-starter:1.6.0")
    implementation('org.web3j:core:4.12.0'){
        exclude group : 'org.bouncycastle', module : 'bcprov-jdk18on'
        exclude group : 'com.squareup.okhttp3', module : 'okhttp'
    }
    implementation('org.bouncycastle:bcprov-jdk18on:1.78.1')
    implementation('com.squareup.okhttp3:okhttp:4.12.0')

    implementation('org.web3j:web3j-sokt:0.4.0')
    // AWS
    implementation("com.amazonaws:aws-java-sdk-s3:${s3Version}")

    // Kotlin
    implementation("com.fasterxml.jackson.module:jackson-module-kotlin")
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    implementation("org.jetbrains.kotlin:kotlin-reflect")

    // JAX-B dependencies for JDK 9+
    implementation('jakarta.xml.bind:jakarta.xml.bind-api:2.3.2')
    implementation('org.glassfish.jaxb:jaxb-runtime:2.3.2')
    implementation('org.javassist:javassist:3.23.1-GA')

    // Utils
    implementation('commons-io:commons-io:2.8.0')
    implementation 'org.apache.commons:commons-lang3:3.6'

    implementation('org.apache.httpcomponents:httpclient:4.5.13')

    // Test
    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation("org.mockito:mockito-core:2.22.0")

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.3'
    testImplementation("junit:junit:4.13.1")

}

test {
    useJUnitPlatform()
}

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
    main.resources.srcDirs += 'frontend/build/resources'
    main.resources.srcDirs += 'widget/build/resources'
    main.resources.srcDirs += 'tracker/build/resources'
    main.resources.srcDirs += 'payment-chooser/build/resources'
}

// Kotlin
compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "17"
    }
}
compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "17"
    }
}

// Jacoco
jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }

    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    'io/openfuture/api/*Application*',
                    'io/openfuture/api/model/**',
                    'io/openfuture/api/exception/**',
                    'io/openfuture/api/annotation/**',
                    'io/openfuture/api/config/**'
            ])
        })
    }
}
check.dependsOn jacocoTestReport

// Front
//processResources.dependsOn(':frontend:assemble')
//processResources.dependsOn(':widget:assemble')
//processResources.dependsOn(':tracker:assemble')
//processResources.dependsOn(':payment-chooser:assemble')